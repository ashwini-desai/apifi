plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
    id 'org.jetbrains.kotlin.kapt' version '1.3.72' apply false
    id "com.kdabir.mksrc" version "1.1.0"
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.kapt'
    apply plugin: "com.kdabir.mksrc"

    group = 'com.medly.apifi'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        implementation platform('org.jetbrains.kotlin:kotlin-bom')
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        testImplementation 'io.kotest:kotest-runner-junit5-jvm:4.1.1'
        testImplementation 'io.kotest:kotest-assertions-core-jvm:4.1.1'
        testImplementation 'io.kotest:kotest-property-jvm:4.1.1'
        testImplementation 'io.kotest:kotest-runner-console-jvm:4.1.1'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "full"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(":codegen") {

    dependencies {
        implementation 'io.swagger.parser.v3:swagger-parser-v3:2.0.16'
        implementation 'org.openapitools:openapi-generator-cli:3.3.4'
        implementation 'com.squareup:kotlinpoet:1.5.0'

        implementation  'org.slf4j:slf4j-simple:1.7.30'
        implementation 'org.apache.commons:commons-text:1.8'
    }

}

project(":cli") {
    apply plugin: 'application'

    applicationName = 'apifi-codegen'
    mainClassName = "apifi.cli.ApifiCliKt"

    dependencies {
        implementation project(":codegen")
        implementation "com.github.ajalt:clikt:2.7.1"
    }
}

project(":runtime-micronaut") {

    dependencies {
        kapt(platform("io.micronaut:micronaut-bom:$micronautVersion"))
        kapt("io.micronaut:micronaut-inject-java")

        implementation(platform("io.micronaut:micronaut-bom:$micronautVersion"))
        implementation("io.micronaut:micronaut-inject")
        implementation("io.micronaut:micronaut-http")
        implementation("io.micronaut:micronaut-http-server")

        kaptTest("io.micronaut:micronaut-inject-java")
    }
}