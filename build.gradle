plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72' apply false
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'com.medly.apifi'

    repositories {
        jcenter()
    }

    dependencies {
        implementation platform('org.jetbrains.kotlin:kotlin-bom')
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

        testImplementation 'org.jetbrains.kotlin:kotlin-test'
        testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
        testImplementation 'io.kotlintest:kotlintest-runner-junit5:3.4.2'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
            exceptionFormat "full"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

project(":codegen") {
    dependencies {
        implementation 'io.swagger.parser.v3:swagger-parser-v3:2.0.16'
        implementation 'org.openapitools:openapi-generator-cli:3.3.4'
        implementation 'com.squareup:kotlinpoet:1.5.0'

        implementation  'org.slf4j:slf4j-simple:1.7.30'
        implementation 'org.apache.commons:commons-text:1.8'

        testImplementation("io.kotlintest:kotlintest-runner-junit5:3.3.0")
    }
}